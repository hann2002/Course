class Keyboard {
    static Array keyboard;
    /** Initializes the keyboard. */
    function void init() {
        let keyboard = 24576;
        return;
    } 
    function char keyPressed() {
        return keyboard[0];
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        while(Keyboard.keyPressed() = 0){}
        let c = Keyboard.keyPressed();
        while(~(Keyboard.keyPressed() = 0)){}
        if((c < 32) | (c > 126)){
            return c;
        }
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char cha;
        do Output.printString(message);
        let s = String.new(50);
        while(true){
            let cha = Keyboard.readChar();
            if(cha = 128){
                  do Output.println();
                  return s;
            }else{
                  if(cha = 129){
                        do s.eraseLastChar(); 
                  }else{
                        let s = s.appendChar(cha);
                  }
            }
        }
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
