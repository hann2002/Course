class String {
    field Array s;
    field int len, max;
    /** Constructs a new empty String with a maximum length of maxLength. */
    constructor String new(int maxLength) {
        if( maxLength = 0 ) {
            let maxLength = 1;
        }
        let s = Array.new(maxLength);
        let len = 0;
        let max = maxLength;
        return this;
    }

    /** De-allocates the string and frees its space. */
    method void dispose() {
        do Memory.deAlloc(s);
        return;
    }

    /** Returns the current length of this String. */
    method int length() {
        return len;
    }

    /** Returns the character at location j. */
    method char charAt(int j) {
        return s[j];
    }

    /** Sets the j'th character of this string to be c. */
    method void setCharAt(int j, char c) {
        let s[j] = c;
        return;
    }

    /** Appends the character c to the end of this String.
     *  Returns this string as the return value. */
    method String appendChar(char c) {
        if(len < max){
            let s[len] = c;
            let len = len + 1;
        }
        return this;
    }

    /** Erases the last character from this String. */
    method void eraseLastChar() {
        if(len > 0){
            let len = len - 1;
        }
        return;
    }

    /** Returns the integer value of this String until the first non
     *  numeric character. */
    method int intValue() {
        var int i, sum;
        var boolean neg;
        let sum = 0;
        if ((len > 0) & (s[0] = 45)) { 
            let neg = true;
            let i = 1;
        }
        else {
            let neg = false;
            let i = 0;
        }
        while (i < len) {
            if (s[i] > 47 & s[i] < 58) { 
                let sum = (sum * 10) + (s[i] - 48);
            }
            let i = i + 1;
        }
        if (neg) {
            return -sum;
        }
        else {
            return sum;
        }
    }

    /** Sets this String to hold a representation of the given number. */
    method void setInt(int number) {
        var int mod, div;
        let len = 0;
        if (number < 0) {
            do appendChar(45); 
            let number = -number;
        }
        let div = number / 10;
        let mod = number - (div * 10);
        if (number < 10){
            do appendChar(mod + 48);
        }
        else{
            do setInt(div);
            do appendChar(mod + 48);
        }
        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}